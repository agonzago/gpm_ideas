SUBROUTINE DSOLAB(F, P, A, B, N, K, RETCO)
  IMPLICIT NONE   
  INTEGER , INTENT(IN) :: N,K
  INTEGER , INTENT(OUT) :: RETCO
  DOUBLE PRECISION, INTENT(IN), DIMENSION(N,N) :: A,B  
  DOUBLE PRECISION, INTENT(OUT) ,DIMENSION(K,K) :: P 
  DOUBLE PRECISION, INTENT(OUT) ,DIMENSION(N-K,K) :: F
  
  LOGICAL FUNCG2
  EXTERNAL FUNCG2
  
  DOUBLE PRECISION, DIMENSION(N) ::  ALPHAR, ALPHAI, BETA
  DOUBLE PRECISION, DIMENSION(N,N) :: VSL, VSR
  DOUBLE PRECISION, DIMENSION(1) ::  TEMPWORK
  DOUBLE PRECISION, ALLOCATABLE :: WORK(:), Z11WORK(:)
  INTEGER LWORK
  INTEGER SDIM, I,J
  LOGICAL, DIMENSION(N) :: BWORK
  INTEGER, DIMENSION(K) :: IPIV
  
  DOUBLE PRECISION, DIMENSION(K,K) :: Z11, Z11I, S11I, T11, PP
  DOUBLE PRECISION, DIMENSION(N-K,K) :: Z21
  DOUBLE PRECISION, DIMENSION(N,N) :: S,T
  
  S = A
  T = B

  LWORK = -1 
  CALL DGGES( 'N', 'V', 'S', FUNCG2, N, S, N, T, N,&
       & SDIM, ALPHAR, ALPHAI, BETA, VSL, N, VSR,&
       & N, TEMPWORK, LWORK, BWORK, RETCO )
  IF ( RETCO .NE. 0 ) THEN
     RETURN 
  END IF
  
  LWORK = INT(TEMPWORK(1))
  ALLOCATE(WORK(LWORK))  
  CALL DGGES( 'N', 'V', 'S', FUNCG2, N, S, N, T, N,&
       & SDIM, ALPHAR, ALPHAI, BETA, VSL, N, VSR,&
       & N, WORK, LWORK, BWORK, RETCO )
  
  DEALLOCATE(WORK)

  IF ( SDIM .NE. K ) THEN
     PRINT*, 'ERROR: WRONG NUMBER OF STABLE EIGENVALUES'
     RETCO = 1
     RETURN 
  END IF

  Z11  = VSR(1:K,1:K)  
  Z11I = VSR(1:K,1:K)
  Z21  = VSR(K+1:N,1:K)
  S11I = S(1:K,1:K)
  T11  = T(1:K,1:K)

  ! LU FACTORIZATION
  CALL DGETRF(K,K,Z11I,K,IPIV,RETCO)  
  IF ( RETCO .NE. 0 ) THEN 
     RETURN 
  END IF
  
  
  LWORK = -1    
  CALL DGETRI(K,Z11I,K,IPIV,TEMPWORK,LWORK,RETCO)  
  IF ( RETCO .NE. 0 ) THEN 
     RETURN 
  END IF
  
  LWORK = INT(TEMPWORK(1))
  ALLOCATE(Z11WORK(LWORK))
  CALL DGETRI(K,Z11I,K,IPIV,Z11WORK,LWORK,RETCO)
  IF ( RETCO .NE. 0 ) THEN 
     RETURN 
  END IF

  DEALLOCATE(Z11WORK)
  
  IPIV = 0
  CALL DGETRF(K,K,S11I,K,IPIV,RETCO)  
  IF ( RETCO .NE. 0 ) THEN 
     RETURN 
  END IF
  
  LWORK = -1      
  CALL DGETRI(K,S11I,K,IPIV,TEMPWORK,LWORK,RETCO)  
  IF ( RETCO .NE. 0 ) THEN 
     RETURN 
  END IF
  
  LWORK = INT(TEMPWORK(1))
  ALLOCATE(Z11WORK(LWORK))
  CALL DGETRI(K,S11I,K,IPIV,Z11WORK,LWORK,RETCO)
  IF ( RETCO .NE. 0 ) THEN 
     RETURN 
  END IF
  
  
  CALL DGEMM('N','N',N-K,K,K,DBLE(1.0),Z21,N-K,Z11I,K,DBLE(0.0), F,N-K)  !F =Z21*(Z11)^-1    
  CALL DGEMM('N','N',K,K,K,DBLE(1.0),S11I,K,T11,K,DBLE(0.0), P,K)  !P = (S11)^-1*T11
  CALL DGEMM('N','N',K,K,K,DBLE(1.0),Z11,K,P,K,DBLE(0.0),PP,K)     !PP = Z11*P = Z11*(S11)^-1*T11
  CALL DGEMM('N','N',K,K,K,DBLE(1.0),PP,K,Z11I,K,DBLE(0.0),P,K)    !P = PP*(Z11)^-1 = Z11*(S11)^-1*T11*(Z11)^-1
  
  DO I = 1, K
     DO J = 1,K
        IF ( ABS(P(I,J)) .LE. 1E-10 )  THEN 
           P(I,J) = 0.0          
        END IF
     END DO
  END DO
  
  DO I = 1, N-K
     DO J = 1,K
        IF ( ABS(F(I,J)) .LE. 1E-10 )  THEN 
           F(I,J) = 0.0          
        END IF
     END DO
  END DO
  
  
  DEALLOCATE(Z11WORK)
  RETURN
END SUBROUTINE DSOLAB


LOGICAL FUNCTION FUNCG2(ALPHAR,ALPHAI, BETA)
  DOUBLE PRECISION ALPHAR,ALPHAI, BETA   
  FUNCG2 = ( (ALPHAR**2 + ALPHAI**2) .GE. (BETA**2) )
END FUNCTION FUNCG2


